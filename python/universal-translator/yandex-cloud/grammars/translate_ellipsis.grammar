root:
    $LeftFiller* [$Phrase? $FromStrict $ToStrict?]
    $LeftFiller* [$Phrase? $FromStrict? $ToStrict]
    $LeftFiller+ [$Phrase $FromStrict? $ToStrict?]

slots:
    from:
        source: $FromLang                   
    to:
        source: $ToLang
    phrase:
        source: $Phrase

filler:
    алиса

$LeftFiller:
    а
    теперь | сейчас
    если

$Phrase:
    .+

$Lang:
    %lemma
    азербайджанский | албанский | амхарский | английский | арабский | армянский | африкаанс | баскский | башкирский | белорусский | бенгальский | бирманский | болгарский | боснийский | валлийский | венгерский | вьетнамский | гаитянский | галисийский | горномарийский | греческий | грузинский | гуджарати | датский | иврит | идиш | индонезийский | ирландский | исландский | испанский | итальянский | казахский | каннада | каталанский | киргизский | китайский | корейский | коса | кхмерский | лаосский | латынь | латышский | литовский | люксембургский | македонский | малагасийский | малайский | малаялам | мальтийский | маори | маратхи | марийский | монгольский | немецкий | непальский | нидерландский | норвежский | панджаби | папьяменто | персидский | польский | португальский | румынский | русский | себуанский | сербский | сингальский | словацкий | словенский | суахили | сунданский | тагальский | таджикский | тайский | тамильский | татарский | телугу | турецкий | удмуртский | узбекский | украинский | урду | финский | французский | хинди | хорватский | чешский | чувашский | шведский | шотландский (гэльский) | эсперанто | эстонский | яванский | японский
    англо | русско | немецко | английско | испанско | украинско

$FromStrict:
    с $FromLang

$ToStrict:
    на $ToLang
    по $ToLang

$From:
    $FromStrict
    $FromLang

$To:
    $ToStrict
    $ToLang

$FromTo:
    $From $To

$FromLang:
    $Lang

$ToLang:
    $Lang

$Translate:
    %lemma
    переведи
    перевод
    как будет
    как переводится
